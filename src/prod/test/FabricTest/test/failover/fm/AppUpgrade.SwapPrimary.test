#
# [owner] MMohsin, AprameyR, AlexWun
#
# This test verifies that primary swap should should only happen for code packages that are getting upgraded.
#

votes 10 20 30
fmservice 3 2
namingservice 1 3 2
cmservice 3 2
cleantest

set DummyPLBEnabled true
set UpgradeStatusPollInterval 5
set UserReplicaRestartWaitDuration 9999

# Avoid automatic cleanup of unused application types
!setcfg Management.CleanupUnusedApplicationTypes=false

+10 ud=UD1
+20 ud=UD2
+30 ud=UD3
verify

#
# Application version 1.0
#
app.add version10 TestApp 1.0
app.clear version10
app.servicepack version10 ServicePackage-A version=1.0
app.servicetypes version10 ServicePackage-A ServiceType-A.A stateful
app.servicetypes version10 ServicePackage-A ServiceType-A.B stateful
app.codepack version10 ServicePackage-A Code-A.A types=ServiceType-A.A version=1.0
app.codepack version10 ServicePackage-A Code-A.B types=ServiceType-A.B version=1.0
app.upload version10
provisionapp version10

#
# Application version 1.1
#
app.add version11 TestApp 1.1
app.clear version11
app.servicepack version11 ServicePackage-A version=1.1
app.servicetypes version11 ServicePackage-A ServiceType-A.A stateful
app.servicetypes version11 ServicePackage-A ServiceType-A.B stateful
app.codepack version11 ServicePackage-A Code-A.A types=ServiceType-A.A version=1.1
app.codepack version11 ServicePackage-A Code-A.B types=ServiceType-A.B version=1.0
app.upload version11
provisionapp version11

#
# Application version 1.2
#
app.add version12 TestApp 1.2
app.clear version12
app.servicepack version12 ServicePackage-A version=1.2
app.servicetypes version12 ServicePackage-A ServiceType-A.A stateful
app.servicetypes version12 ServicePackage-A ServiceType-A.B stateful
app.codepack version12 ServicePackage-A Code-A.A types=ServiceType-A.A version=1.2
app.codepack version12 ServicePackage-A Code-A.B types=ServiceType-A.B version=1.0
app.upload version12
provisionapp version12

# Create application version 1.0
createapp fabric:/MyApp TestApp 1.0
verify

# Create services
createservice fabric:/MyApp/Volatile1 ServiceType-A.A y 1 3 appname=fabric:/MyApp
createservice fabric:/MyApp/Volatile2 ServiceType-A.B y 1 3 appname=fabric:/MyApp
verify

# Capture the epoch for the fabric:/MyApp/Volatile2 service
!var Volatile2Epoch FM.FT.Epoch.fabric:/MyApp/Volatile2

#
# Perform application upgrade from version 1.0 to 1.1
#
upgradeapp fabric:/MyApp 1.1 Rolling
verifyupgradeapp fabric:/MyApp

verify

# Verify that no primary swap happened for fabric:/MyApp/Volatile2
!waitforstate FM.FT.Epoch.fabric:/MyApp/Volatile2 <var.Volatile2Epoch>

#
# Start upgrade from version 1.1 to 1.2, and then rollback at the last UD
#
upgradeapp fabric:/MyApp 1.2 Rolling upgrademode=manual

verifyupgradeapp fabric:/MyApp upgradedomains=UD1
upgradeappmovenext fabric:/MyApp

verifyupgradeapp fabric:/MyApp upgradedomains=UD1,UD2
upgradeappmovenext fabric:/MyApp

rollbackapp fabric:/MyApp
updateappupgrade fabric:/MyApp upgrademode=auto
setrollbackapp fabric:/MyApp 1.1 failurereason=Interrupted

verifyupgradeapp fabric:/MyApp

verify

# Verify that no primary swap happened for fabric:/MyApp/Volatile2
!waitforstate FM.FT.Epoch.fabric:/MyApp/Volatile2 <var.Volatile2Epoch>

-*

!q
