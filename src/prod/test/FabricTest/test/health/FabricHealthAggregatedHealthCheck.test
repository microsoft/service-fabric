#
# Checks aggregated health for entities,
# using different policies and filters.
# Creates dummy entities instead of using System entities, so checks are not affected by new health reports sent by system components.
# Checks unhealthy evaluations, with trimming when needed.
#
votes 10
cmservice 3 1
namingservice 1 1 1
fmservice 1 1
set DummyPLBEnabled true
set HealthOperationTimeout 5
set HealthReportSendInterval 0
set HealthReportRetrySendInterval 1
set QueryOperationRetryDelay 1

# Disable slow commits reports so when test runs under load health isn't impacted
set SlowCommitCountThreshold 100000
set GracefulReplicaShutdownMaxDuration 0

cleantest
+10
+20
+30
+40
+50
verify

# Wait for system reports to arrive at HM
queryhealth cluster expectedhealthstate=ok expectedstates=nodes-ok:5;apps-ok:1 includeSystemAppStats=false stats=nodes-ok:5;apps-0;services-0;partitions-0;replicas-0;deployedapps-0;deployedservicepackages-0

########################################
# Testcase 1
# Report health on an entity with different states 
# and check aggregated health state 
# Entity has no children
########################################
reporthealth node nodeid=10 sequencenumber=1 healthstate=ok
queryhealth node nodeid=10 expectedhealthstate=ok

reporthealth node nodeid=10 sequencenumber=2 healthstate=error
queryhealth node nodeid=10 expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property 
queryhealth node nodeid=10 eventsfilter=error expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property expectedeventcount=1
queryhealth node nodeid=10 eventsfilter=warning expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property expectedeventcount=0
queryhealth node nodeid=10 eventsfilter=warning|error expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property expectedeventcount=1

queryhealth cluster nodesfilter=warning expectedhealthstate=error expectedstates=nodes-warning:0,ok:0,error:0 
queryhealth cluster nodesfilter=all expectedhealthstate=error expectedstates=nodes-error:1,ok:4
queryhealth cluster nodesfilter=error|warning expectedhealthstate=error expectedstates=nodes-error:1
queryhealth cluster nodesfilter=error expectedhealthstate=error expectedstates=nodes-error:1
queryhealth cluster nodesfilter=error|ok|warning expectedhealthstate=error expectedstates=nodes-error:1,ok:4
queryhealth cluster nodesfilter=none expectedhealthstate=error expectedstates=nodes-error:0,ok:0,warning:0
queryhealth cluster nodesfilter=ok expectedhealthstate=error expectedstates=nodes-ok:4 includeSystemAppStats=false stats=nodes-ok:4,error:1;apps-0

reporthealth node nodeid=10 sequencenumber=3 healthstate=warning timetoliveseconds=20 transient=true
queryhealth node nodeid=10 expectedhealthstate=warning expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth cluster expectedstates=nodes-ok:4,warning:1 includeSystemAppStats=false stats=nodes-ok:4,warning:1;apps-0

queryhealth node nodeid=10 clusterpolicy=warningaserror expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth cluster clusterpolicy=warningaserror expectedstates=nodes-ok:4,error:1

# wait for the transient event to expire.
# increase the query retry time to allow for the longer timetolive to the last report. 
# timetolive of last report was increased to accomodate slower test runs.
set QueryOperationRetryCount 20
queryhealth cluster expectedstates=nodes-ok:5 stats=nodes-ok:5 

# set it back to the default:
set QueryOperationRetryCount 15

reporthealth node nodeid=10 sequencenumber=4 healthstate=warning timetoliveseconds=5
# wait for the event to expire
queryhealth node nodeid=10 expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
# Expired event is evaluated at error, regardless of its original health state
queryhealth node nodeid=10 eventsfilter=warning expectedhealthstate=error expectedeventcount=0
queryhealth node nodeid=10 eventsfilter=error expectedhealthstate=error expectedeventcount=1
queryhealth cluster expectedstates=nodes-ok:4,error:1 stats=nodes-ok:4,error:1

queryhealth cluster expectedhealthstate=error expectedreason=nodes,1,0
queryhealth cluster expectedhealthstate=warning clusterpolicy=maxunhealthynodespercent:30 expectedreason=nodes,1,30

########################################
# Testcase 2
# Report health for different properties on an entity and check aggregated health state 
# Entity has no children
########################################

reporthealth node nodeid=20 sequencenumber=1 healthstate=ok property=property1
reporthealth node nodeid=20 sequencenumber=1 healthstate=ok property=property2
reporthealth node nodeid=20 sequencenumber=1 healthstate=warning property=property3 timetoliveseconds=10 transient=true
reporthealth node nodeid=20 sequencenumber=1 healthstate=error property=property4 timetoliveseconds=5 transient=true

queryhealth node nodeid=20 eventsfilter=error expectedhealthstate=error expectedreason=event,FabricTest_Source,property4 expectedeventcount=1

# Node 10 is at error (default)
queryhealth cluster expectedstates=nodes-ok:3,error:2 stats=nodes-ok:3,error:2

# wait for the warning event to expire
queryhealth node nodeid=20 expectedhealthstate=ok
queryhealth cluster expectedstates=nodes-ok:4,error:1 stats=nodes-ok:4,error:1

# Replace the expired events with ok
reporthealth node nodeid=20 sequencenumber=2 healthstate=ok property=property3
reporthealth node nodeid=20 sequencenumber=2 healthstate=ok property=property4

# Replace the ok events, check state
reporthealth node nodeid=20 sequencenumber=2 healthstate=warning property=property1
queryhealth node nodeid=20 expectedhealthstate=warning expectedreason=event,FabricTest_Source,property1
reporthealth node nodeid=20 sequencenumber=2 healthstate=error property=property2
queryhealth node nodeid=20 expectedhealthstate=error expectedreason=event,FabricTest_Source,property2
queryhealth cluster expectedstates=nodes-ok:3,error:2

########################################
# Create App with 2 service types and 2 services
# Build dummy entities and make sure the hierarchy is correctly built
########################################

# Stateful service with 3 partitions and 2 replicas per partition

reporthealthinternal node nodeid=60 node.instanceid=1 sequencenumber=1 healthstate=ok ud=ud1 fd=fd1 ipaddressorfqdn=127.0.0.1:3333 sourceid=System.FM
reporthealthinternal node nodeid=70 node.instanceid=1 sequencenumber=1 healthstate=ok ud=ud1 fd=fd1 ipaddressorfqdn=127.0.0.1:3333 sourceid=System.FM
queryhealth node nodeid=60 expectedhealthstate=ok
queryhealth node nodeid=70 expectedhealthstate=ok

!string SvcTypeA1Partition0 31299114-0820-4cbd-8090-3ba86b899db2
!string SvcTypeA1Partition1 1b822f7a-291d-4afc-b143-7830dc44454e
!string SvcTypeA1Partition2 a282ce11-2be7-4be6-8dc2-18002a24f15d

reporthealthinternal application appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.CM apppolicy=default
reporthealthinternal service servicename=fabric:/app1/ServiceA1 appname=fabric:/app1 servicetypename=ServiceTypeA1 sequencenumber=1 healthstate=ok sourceid=System.FM

reporthealthinternal partition partitionguid=<string.SvcTypeA1Partition0> servicename=fabric:/app1/ServiceA1 type=persistent sequencenumber=1 healthstate=ok sourceid=System.FM
reporthealthinternal partition partitionguid=<string.SvcTypeA1Partition1> servicename=fabric:/app1/ServiceA1 type=persistent sequencenumber=1 healthstate=ok sourceid=System.FM
reporthealthinternal partition partitionguid=<string.SvcTypeA1Partition2> servicename=fabric:/app1/ServiceA1 type=persistent sequencenumber=1 healthstate=ok sourceid=System.FM

reporthealthinternal replica replica.id=10000 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition0> nodeid=20 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal replica replica.id=10001 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition0> nodeid=30 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal replica replica.id=10002 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition1> nodeid=40 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal replica replica.id=10003 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition1> nodeid=50 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal replica replica.id=10004 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition2> nodeid=60 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal replica replica.id=10005 replica.instanceid=1 partitionguid=<string.SvcTypeA1Partition2> nodeid=70 sequencenumber=1 healthstate=ok sourceid=System.RA

reporthealthinternal deployedapplication nodeid=20 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=30 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=40 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=50 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=60 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=70 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting

reporthealthinternal deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=30 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=40 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=50 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=60 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=70 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting

# Stateless service with 1 partition and 5 replicas
!string SvcTypeB1Partition0 0b94cb8f-9889-4357-a9ef-e5c7c3324f44

reporthealthinternal service servicename=fabric:/app1/ServiceB1 appname=fabric:/app1 servicetypename=ServiceTypeB1 sequencenumber=1 healthstate=ok sourceid=System.FM

reporthealthinternal partition partitionguid=<string.SvcTypeB1Partition0> servicename=fabric:/app1/ServiceB1 type=stateless sequencenumber=1 healthstate=ok sourceid=System.FM

reporthealthinternal instance replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> nodeid=20 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal instance replica.id=20001 partitionguid=<string.SvcTypeB1Partition0> nodeid=30 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal instance replica.id=20002 partitionguid=<string.SvcTypeB1Partition0> nodeid=40 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal instance replica.id=20003 partitionguid=<string.SvcTypeB1Partition0> nodeid=50 sequencenumber=1 healthstate=ok sourceid=System.RA
reporthealthinternal instance replica.id=20004 partitionguid=<string.SvcTypeB1Partition0> nodeid=60 sequencenumber=1 healthstate=ok sourceid=System.RA

reporthealthinternal deployedapplication nodeid=20 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=30 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=40 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=50 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedapplication nodeid=60 appname=fabric:/app1 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting

reporthealthinternal deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageB1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=30 appname=fabric:/app1 servicemanifestname=ServicePackageB1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=40 appname=fabric:/app1 servicemanifestname=ServicePackageB1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=50 appname=fabric:/app1 servicemanifestname=ServicePackageB1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting
reporthealthinternal deployedservicepackage nodeid=60 appname=fabric:/app1 servicemanifestname=ServicePackageB1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.Hosting

# Query health of the system to make sure entities are correctly created
queryhealth partition partitionguid=<string.SvcTypeA1Partition0> expectedhealthstate=ok expectedstates=ok:2 stats=replicas-ok:2
queryhealth partition partitionguid=<string.SvcTypeA1Partition1> expectedhealthstate=ok expectedstates=ok:2 stats=replicas-ok:2
queryhealth partition partitionguid=<string.SvcTypeA1Partition2> expectedhealthstate=ok expectedstates=ok:2 stats=replicas-ok:2
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=ok expectedstates=ok:5 excludeHealthStats=true stats=0
queryhealth deployedapplication appname=fabric:/app1 nodeid=20 expectedhealthstate=ok expectedstates=ok:2 stats=deployedservicepackages-ok:2
queryhealth service servicename=fabric:/app1/ServiceA1 expectedhealthstate=ok expectedstates=ok:3 stats=partitions-ok:3
queryhealth service servicename=fabric:/app1/ServiceB1 expectedhealthstate=ok expectedstates=ok:1

# Application has 2 service children and 6 deployed applications (equal to number of nodes)
queryhealth application appname=fabric:/app1 expectedhealthstate=ok expectedstates=services-ok:2;deployedapps-ok:6 \
    stats=services-ok:2;partitions-ok:4;replicas-ok:11;deployedapps-ok:6;deployedservicepackages-ok:11

queryhealth cluster stats=apps-ok:1;services-ok:2;partitions-ok:4;replicas-ok:11;deployedapps-ok:6;deployedservicepackages-ok:11

########################################
# Testcase 3
# Service with 1 partition and 5 replicas
# Report different health on replicas, check partition and service health
########################################
reporthealth partition partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=ok
reporthealth instance replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=error
reporthealth instance replica.id=20001 partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=ok
reporthealth instance replica.id=20002 partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=ok
reporthealth instance replica.id=20003 partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=warning
reporthealth instance replica.id=20004 partitionguid=<string.SvcTypeB1Partition0> sequencenumber=1 healthstate=error

queryhealth replica replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth replica replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> eventsfilter=warning expectedhealthstate=error expectedeventcount=0
queryhealth replica replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> eventsfilter=ok|error expectedhealthstate=error expectedeventcount=2
queryhealth replica replica.id=20001 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=ok
queryhealth replica replica.id=20002 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=ok
queryhealth replica replica.id=20003 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=warning expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth replica replica.id=20004 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property

queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedstates=ok:2,warning:1,error:2

queryhealth partition partitionguid=<string.SvcTypeB1Partition0> replicasfilter=warning expectedstates=ok:0,warning:1,error:0
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> replicasfilter=warning|error expectedstates=ok:0,warning:1,error:2

queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=replicas,2,0

queryhealth replica replica.id=20003 partitionguid=<string.SvcTypeB1Partition0> apppolicy=warningaserror expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> apppolicy=warningaserror expectedstates=ok:2,error:3

queryhealth partition partitionguid=<string.SvcTypeB1Partition0> apppolicy=maxunhealthyreplicaspercent:10 expectedhealthstate=error expectedstates=ok:2,warning:1,error:2 expectedreason=replicas,2,10
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> apppolicy=maxunhealthyreplicaspercent:50 expectedhealthstate=warning expectedstates=ok:2,warning:1,error:2 expectedreason=replicas,3,50
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> apppolicy=maxunhealthyreplicaspercent:40,warningaserror expectedhealthstate=error expectedstates=ok:2,error:3 expectedreason=replicas,3,40

queryhealth service servicename=fabric:/app1/ServiceB1 apppolicy=maxunhealthypartitionspercent:0,maxunhealthyreplicaspercent:10 expectedhealthstate=error expectedstates=error:1 expectedreason=partitions,1,0
queryhealth service servicename=fabric:/app1/ServiceB1 partitionsfilter=warning|error expectedstates=error:1

queryhealth service servicename=fabric:/app1/ServiceB1 apppolicy=maxunhealthypartitionspercent:10,maxunhealthyreplicaspercent:50 expectedhealthstate=warning expectedstates=warning:1 expectedreason=partitions,1,10

queryhealth deployedapplication appname=fabric:/app1 nodeid=20 expectedhealthstate=ok expectedstates=ok:2
queryhealth deployedapplication appname=fabric:/app1 nodeid=70 expectedhealthstate=ok expectedstates=ok:1
queryhealth deployedapplication appname=fabric:/app1 nodeid=10 expectempty=true

queryhealth cluster expectedstates=apps-error:1,ok:1 \
    stats=apps-error:1;services-ok:1,error:1;partitions-ok:3,error:1;replicas-ok:8,warning:1,error:2;deployedapps-ok:6;deployedservicepackages-ok:11

!string jsoapppolicies1 {"ApplicationHealthPolicyMap":[{\
    "Key":"fabric:/app1",\
    "Value":{"DefaultServiceTypeHealthPolicy":{"MaxPercentUnhealthyServices":75}}\
    }]}

queryhealth cluster apphealthpolicies=<string.jsoapppolicies1> expectedstates=apps-warning:1,ok:1

########################################
# Testcase 4
# Service with 3 partitions and 2 replicas
# Report different health on deployed entities, check app service type and app health
########################################
reporthealthinternal deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=warning
reporthealthinternal deployedservicepackage nodeid=30 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok
reporthealthinternal deployedservicepackage nodeid=40 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok
reporthealthinternal deployedservicepackage nodeid=50 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok
reporthealthinternal deployedservicepackage nodeid=60 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=error
reporthealthinternal deployedservicepackage nodeid=70 appname=fabric:/app1 servicemanifestname=ServicePackageA1 servicepackageinstanceid=1 sequencenumber=1 healthstate=ok

queryhealth deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=warning expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth deployedservicepackage nodeid=30 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=ok 
queryhealth deployedservicepackage nodeid=40 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=ok 
queryhealth deployedservicepackage nodeid=50 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=ok 
queryhealth deployedservicepackage nodeid=60 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth deployedservicepackage nodeid=60 appname=fabric:/app1 servicemanifestname=ServicePackageA1 eventsfilter=warning expectedhealthstate=error expectedeventcount=0
queryhealth deployedservicepackage nodeid=70 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=ok 

queryhealth deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 apppolicy=warningaserror expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property

queryhealth deployedapplication appname=fabric:/app1 nodeid=20 expectedhealthstate=warning expectedstates=ok:1,warning:1 expectedreason=deployedservicepackages,1
queryhealth deployedapplication appname=fabric:/app1 nodeid=20 deployedservicepackagesfilter=warning expectedhealthstate=warning expectedstates=ok:0,warning:1 
queryhealth deployedapplication appname=fabric:/app1 nodeid=20 deployedservicepackagesfilter=warning|ok expectedhealthstate=warning expectedstates=ok:1,warning:1 
queryhealth deployedapplication appname=fabric:/app1 nodeid=20 deployedservicepackagesfilter=ok expectedhealthstate=warning expectedstates=ok:1 

queryhealth deployedapplication appname=fabric:/app1 nodeid=20 apppolicy=warningaserror expectedhealthstate=error expectedstates=ok:1,error:1 expectedreason=deployedservicepackages,1

queryhealth deployedapplication appname=fabric:/app1 nodeid=60 expectedhealthstate=error expectedstates=ok:1,error:1 expectedreason=deployedservicepackages,1

queryhealth service servicename=fabric:/app1/ServiceA1 expectedhealthstate=ok

queryhealth cluster expectedhealthstate=error stats=apps-error:1;services-ok:1,error:1;partitions-ok:3,error:1;replicas-ok:8,warning:1,error:2;deployedapps-ok:4,warning:1,error:1;deployedservicepackages-ok:9,warning:1,error:1

########################################
# Testcase 5
# App with 2 service types, A1 (unhealthy) and B1 (healthy)
# and 2 services, A1 (healthy) and B1 (unhealthy)
# Check application health
########################################
queryhealth application appname=fabric:/app1 expectedstates=services-ok:1,error:1
queryhealth application appname=fabric:/app1 servicesfilter=warning|error expectedstates=services-error:1
queryhealth application appname=fabric:/app1 servicesfilter=warning expectedstates=services-warning:0

queryhealth application appname=fabric:/app1 apppolicy=maxunhealthypartitionspercent:10,maxunhealthyreplicaspercent:50 expectedstates=services-ok:1,warning:1

queryhealth application appname=fabric:/app1 expectedstates=deployedapps-ok:4,warning:1,error:1
queryhealth application appname=fabric:/app1 deployedapplicationsfilter=error expectedstates=deployedapps-ok:0,warning:0,error:1

queryhealth application appname=fabric:/app1 expectedhealthstate=error expectedstates=ok:5,warning:1,error:2 expectedreason=services,ServiceTypeB1,1,0

queryhealth application appname=fabric:/app1 apppolicy=warningaserror,maxunhealthyservicespercent:20,maxunhealthydeployedapplicationspercent:10 expectedhealthstate=error expectedstates=ok:5,error:3 expectedreason=deployedapplications,2,10

queryhealth cluster apphealthpolicies=<string.jsoapppolicies1> expectedstates=apps-error:1,ok:1

!string jsoapppolicies2 {"ApplicationHealthPolicyMap":[{\
    "Key":"fabric:/app1",\
    "Value":{\
        "MaxPercentUnhealthyDeployedApplications":80,\
        "DefaultServiceTypeHealthPolicy":{"MaxPercentUnhealthyServices":75}\
            }\
    }]}

queryhealth cluster apphealthpolicies=<string.jsoapppolicies2> expectedstates=apps-warning:1,ok:1

###########################################
# Testcase 6: Report new application with and without app policy
############################################
!string App2SvcTypeA1Partition0 403d7c96-6d4a-4ce9-beb0-6f83f17392c4

reporthealthinternal node nodeid=555 node.instanceid=1 sequencenumber=1 healthstate=ok ud=ud1 fd=fd1 ipaddressorfqdn=127.0.0.1:3333 sourceid=System.FM
reporthealthinternal replica replica.id=10000 replica.instanceid=1 partitionguid=<string.App2SvcTypeA1Partition0> nodeid=555 node.instanceid=1 sequencenumber=1 healthstate=warning sourceid=System.RA
reporthealthinternal partition partitionguid=<string.App2SvcTypeA1Partition0> servicename=fabric:/app2/ServiceA1 type=persistent sequencenumber=1 healthstate=ok sourceid=System.FM
reporthealthinternal service servicename=fabric:/app2/ServiceA1 appname=fabric:/app2 servicetypename=ServiceTypeA1 sequencenumber=1 healthstate=ok sourceid=System.FM

# No app policy
reporthealthinternal application appname=fabric:/app2 appinstanceid=1 sequencenumber=1 healthstate=ok sourceid=System.CM 

queryhealth node nodeid=555 expectedhealthstate=ok expectedeventcount=1

queryhealth partition partitionguid=<string.App2SvcTypeA1Partition0> expectedempty=true
queryhealth replica replica.id=10000 partitionguid=<string.App2SvcTypeA1Partition0> expectempty=true
queryhealth service servicename=fabric:/app2/ServiceA1 expectempty=true
queryhealth application appname=fabric:/app2 expectempty=true

# Default app policy
reporthealthinternal application appname=fabric:/app2 appinstanceid=1 sequencenumber=2 healthstate=ok sourceid=System.CM apppolicy=default

queryhealth partition partitionguid=<string.App2SvcTypeA1Partition0> expectedeventcount=1 expectedhealthstate=warning 
queryhealth replica replica.id=10000 partitionguid=<string.App2SvcTypeA1Partition0> expectedeventcount=1 expectedhealthstate=warning expectedreason=event,System.RA,FabricTest_Property 
queryhealth service servicename=fabric:/app2/ServiceA1 expectedeventcount=1 expectedhealthstate=warning
queryhealth application appname=fabric:/app2 expectedeventcount=1 expectedhealthstate=warning

queryhealth cluster expectedstates=apps-error:1,ok:1,warning:1 \
    stats=apps-error:1,warning:1;services-ok:1,warning:1,error:1;partitions-ok:3,warning:1,error:1;replicas-ok:8,warning:2,error:2;deployedapps-ok:4,warning:1,error:1;deployedservicepackages-ok:9,warning:1,error:1

!string jsoapppolicies3 {"ApplicationHealthPolicyMap":[\
    {"Key":"fabric:/app1",\
    "Value":{"MaxPercentUnhealthyDeployedApplications":80,"DefaultServiceTypeHealthPolicy":{"MaxPercentUnhealthyServices":75}}},\
    {"Key":"fabric:/app2",\
    "Value":{"ConsiderWarningAsError":true}}\
    ]}

queryhealth cluster apphealthpolicies=<string.jsoapppolicies3> expectedstates=apps-warning:1,ok:1,error:1

!string jsoapppolicies4 {"ApplicationHealthPolicyMap":[{\
    "Key":"fabric:/app2",\
    "Value":{"ConsiderWarningAsError":true}\
     }]}

queryhealth cluster apphealthpolicies=<string.jsoapppolicies4> expectedstates=apps-ok:1,error:2

# Replace app policy
reporthealthinternal application appname=fabric:/app2 appinstanceid=1 sequencenumber=3 healthstate=ok sourceid=System.CM apppolicy=warningaserror

queryhealth partition partitionguid=<string.App2SvcTypeA1Partition0> expectedeventcount=1 expectedhealthstate=error expectedreason=replicas,1,0
queryhealth replica replica.id=10000 partitionguid=<string.App2SvcTypeA1Partition0> expectedeventcount=1 expectedhealthstate=error 
queryhealth service servicename=fabric:/app2/ServiceA1 expectedeventcount=1 expectedhealthstate=error expectedreason=partitions,1,0
queryhealth application appname=fabric:/app2 expectedeventcount=1 expectedhealthstate=error expectedreason=services,ServiceTypeA1,1,0

queryhealth cluster applicationsfilter=warning|error expectedstates=apps-error:2
queryhealth cluster applicationsfilter=error expectedstates=apps-error:2 \
    stats=apps-error:2;services-ok:1,error:2;partitions-ok:3,warning:0,error:2;replicas-ok:8,warning:1,error:3;deployedapps-ok:4,warning:1,error:1;deployedservicepackages-ok:9,warning:1,error:1

###########################################
# Testcase 7: Trim unhealthy evaluations
############################################
set HMMaxMessageSize 4000
set HMMessageContentBufferRatio 0.75

queryhealth node nodeid=10 expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth replica replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=replicas,2,0
queryhealth service servicename=fabric:/app1/ServiceB1 expectedhealthstate=error expectedreason=partitions,1,0
queryhealth application appname=fabric:/app2 expectedhealthstate=error expectedreason=services,ServiceTypeA1,1,0
queryhealth deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectedhealthstate=warning expectedreason=event,FabricTest_Source,FabricTest_Property
queryhealth deployedapplication appname=fabric:/app1 nodeid=60 expectedhealthstate=error expectedreason=deployedservicepackages,1

set HMMessageContentBufferRatio 0.1

#
# Unhealthy evaluations contain the EventHealthEvaluation.
# The description event doesn't fit in the content buffer ratio, health query returns error
#
queryhealth node nodeid=10 expectederror=EntryTooLarge
queryhealth replica replica.id=20000 partitionguid=<string.SvcTypeB1Partition0> expectederror=EntryTooLarge
queryhealth deployedservicepackage nodeid=20 appname=fabric:/app1 servicemanifestname=ServicePackageA1 expectederror=EntryTooLarge

#
# Unhealthy evaluations have multiple children.
# If description for the outer evaluation doesn't fit, return error.
#
queryhealth cluster expectederror=EntryTooLarge
queryhealth application appname=fabric:/app1 expectederror=EntryTooLarge
queryhealth service servicename=fabric:/app1/ServiceA1 expectederror=EntryTooLarge
queryhealth deployedapplication appname=fabric:/app1 nodeid=20 expectederror=EntryTooLarge
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectederror=EntryTooLarge

#
# Unhealthy evaluations have multiple children.
# Description fits the message, the rest of the evaluation is trimmed.
#
set HMMessageContentBufferRatio 0.22

queryhealth cluster expectederror=EntryTooLarge
queryhealth application appname=fabric:/app1 expectederror=EntryTooLarge

# 2 replicas in error, but the unhealthy evaluations are trimmed
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=replicas,0,0

# 1 partition is in error, but the children are not included in the reply because the evaluations are trimmed
queryhealth service servicename=fabric:/app1/ServiceB1 expectedhealthstate=error expectedreason=partitions,1,0

# service unhealthy, evaluations are trimmed so no partition health evaluation is contained.
queryhealth application appname=fabric:/app2 expectedhealthstate=error expectedreason=services,ServiceTypeA1,0,0

# Description is set, no dsp unhealthy evaluation fits the message
queryhealth deployedapplication appname=fabric:/app1 nodeid=60 expectedhealthstate=error expectedreason=deployedservicepackages,0

#
# Increase the allowed max size, so more children evaluations fit
#
set HMMessageContentBufferRatio 0.5

# Both replicas are captured in the unhealthy evaluations, but only one displays the event.
queryhealth partition partitionguid=<string.SvcTypeB1Partition0> expectedhealthstate=error expectedreason=replicas,2,0

# Last replica health evaluation doesn't contain the event evaluation.
queryhealth service servicename=fabric:/app1/ServiceB1 expectedhealthstate=error expectedreason=partitions,1,0

queryhealth deployedapplication appname=fabric:/app1 nodeid=60 expectedhealthstate=error expectedreason=deployedservicepackages,1

queryhealth application appname=fabric:/app2 expectedhealthstate=error expectedreason=services,ServiceTypeA1,1,0
queryhealth application appname=fabric:/app1 expectedhealthstate=error expectedreason=services,ServiceTypeB1,1,0

queryhealth cluster expectedhealthstate=error expectedreason=nodes,2,0

!q
    